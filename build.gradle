/*
 * Copyright 2013-2014 Paul St√∂hr
 *
 * This file is part of TD.
 *
 * TD is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.0.0'
        classpath 'com.jakewharton.hugo:hugo-plugin:1.1.0'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'hugo'

repositories {
    mavenCentral()
    flatDir { dirs 'libs' }
}

dependencies {
    //Compat
    compile 'com.android.support:support-v4:21.0.3'
    compile 'com.android.support:appcompat-v7:21.0.3'
    compile 'com.github.machinarius:preferencefragment:0.1.1'

    //3th Party
    compile 'org.apache.commons:commons-lang3:3.2.1'
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'com.squareup.retrofit:retrofit:1.8.0'
    compile 'com.squareup.retrofit:converter-jackson:1.8.0'
    compile 'com.jakewharton:butterknife:6.0.0'
    compile('de.psdev.licensesdialog:licensesdialog:1.6.0') {
        exclude module: 'support-v4'
    }

    //JARs
    compile fileTree(dir: 'libs', include: '*.jar')
}

android {
    compileSdkVersion 21
    buildToolsVersion "21.1.1"

    lintOptions {
        //ButterKnife
        disable 'InvalidPackage'
    }

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 21
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    signingConfigs {
        if (project.hasProperty('debugKeystore')) {
            debug {
                storeFile file(debugKeystore)
            }
        }
        if (project.hasProperty('releaseKeystore')) {
            release {
                storeFile file(releaseKeystore)
                storePassword releaseStorePW
                keyAlias releaseKeyalias
                keyPassword releaseKeyPW
            }
        }
    }

    if (project.hasProperty('releaseKeystore')) {
        buildTypes {
            release {
                signingConfig signingConfigs.release
            }
        }
    }
}
