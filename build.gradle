buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:0.7.+'
    }
}

apply plugin: 'android'

android {
    compileSdkVersion 19
    buildToolsVersion "19.0.0"

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs = ['src']
            resources.srcDirs = ['src']
            aidl.srcDirs = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs = ['res']
            assets.srcDirs = ['assets']
        }
    }

    repositories {
        mavenCentral()
        maven { url 'http://192.241.191.41/repo/' }
        maven { url 'http://192.241.191.41/snapshot/' }
    }

    dependencies {
        //Holo
        compile 'com.android.support:support-v4:19.0.+'
        compile 'org.holoeverywhere:library:2.1.1-SNAPSHOT@aar'
        compile 'org.holoeverywhere:addon-preferences:2.1.1-SNAPSHOT@aar'

        //3th Party
        compile 'com.google.code.gson:gson:2.2.4'
        compile 'org.apache.commons:commons-lang3:3.2.1'

        //Projects
        compile project(':lib:VitamioBundle:vitamio')

        //Jars
        compile files('libs/m3u8parser.jar')
    }
}

task copyNativeLibs(type: Copy) {
    from(new File(project(':lib:VitamioBundle:vitamio').getProjectDir(), 'libs')) {
        include '**/*.so'
    }
    into new File(buildDir, 'libs')
}

tasks.withType(Compile) { compileTask -> compileTask.dependsOn copyNativeLibs }

clean.dependsOn 'cleanCopyNativeLibs'

tasks.withType(com.android.build.gradle.tasks.PackageApplication) { pkgTask ->
    pkgTask.jniFolders = new HashSet<File>()
    pkgTask.jniFolders.add(new File(buildDir, 'libs'))
}
